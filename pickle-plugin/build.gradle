plugins {
    id 'java-gradle-plugin'
    id("io.gitlab.arturbosch.detekt").version("$detekt_version")
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

apply plugin: 'kotlin'
apply from: "$rootDir/dependencies.gradle"
apply from: rootProject.file('scripts/publish-info.gradle')

detekt {
    toolVersion = "$detekt_version"
    config = rootProject.files('config/detekt/detekt.yml')
}

repositories {
    google()
    mavenCentral()
}

configurations {
    includeInJar
}


pluginBundle {
    website = 'https://github.com/fourlastor/pickle/'
    vcsUrl = 'https://github.com/fourlastor/pickle/'
    tags = ['testing', 'cucumber', 'pickle']
}

group = ext['PUBLISH_GROUP_ID']
version = ext['PUBLISH_VERSION']

gradlePlugin {
    plugins {
        pickle {
            id = 'io.github.fourlastor.pickle-processor'
            displayName = 'Pickle for Android'
            description = 'An implementation of Cucumber for Android which generates the tests classes at compile time instead of runtime '
            implementationClass = 'com.fourlastor.pickle.PicklePlugin'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.squareup:javapoet:1.11.1'
    implementation "com.android.tools.build:gradle:$agp_version"
    implementation project(':pickle-lib')
    includeInJar project(':pickle-lib')
}

jar {
    from configurations.includeInJar.collect { it.isDirectory() ? it : zipTree(it) }
}
